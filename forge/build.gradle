plugins {
    id 'multiloader-loader'
    id 'net.minecraftforge.gradle' version '7.0.0-rc.2'
    id 'variables'
    id 'net.darkhax.curseforgegradle'
    id 'com.modrinth.minotaur'
}
base {
    archivesName = "${mod_name}-forge-${minecraft_version}"
}

repositories {
    maven minecraft.mavenizer
    maven fg.forgeMaven
    maven fg.minecraftLibsMaven
    maven {
        name = "CurseForge"
        url = "https://cursemaven.com"
    }
    maven { url = 'https://maven.maxhenkel.de/repository/public' }
}

minecraft {
    mappings channel: 'official', version: minecraft_version
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers {
            accessTransformers = "META-INF/accesstransformer.cfg"
        }
    }

    runs {
        register("client") {
            workingDir.convention layout.projectDirectory.dir('runs/client')
        }

        register("server") {
            workingDir.convention layout.projectDirectory.dir('runs/server')
            args '--nogui'
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    implementation minecraft.dependency("net.minecraftforge:forge:${minecraft_version}-${forge_version}")
    // annotationProcessor 'net.minecraftforge:eventbus-validator:7.0-beta.10'

    // Forge's hack fix
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }

    // Simple Voice Chat
    implementation ("curse.maven:simple-voice-chat-416089:7361241")
    implementation "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}"
}

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}


tasks.register('publishCurseForge', net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    dependsOn(tasks.jar)
    apiToken = secrets.curseforgeKey
    group = 'publishing'
    def mainFile = upload(curseforge_project_id, jar)
    mainFile.releaseType = "release"
    mainFile.changelogType = "text"
    mainFile.changelog = rootProject.ext.mod_changelog
    mainFile.addGameVersion(minecraft_version)
    mainFile.addModLoader("Forge")

    mainFile.addRequirement("simple-voice-chat")
}

modrinth {
    token = secrets.modrinthKey
    projectId = "${modrinth_project_id}"
    versionNumber = project.minecraft_version + '-' + project.version
    versionName = "${project.version} for Forge ${project.minecraft_version}"
    versionType = "release"
    uploadFile = jar
    gameVersions = [ project.minecraft_version ]
    changelog = provider { rootProject.ext.mod_changelog }
    loaders = ["forge"]
    dependencies {
        required.project "simple-voice-chat"
    }
}
